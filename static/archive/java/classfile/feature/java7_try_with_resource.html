<!-- 标题 -->
<h1 class="w3-center w3-padding-64 w3-tangerine"><span class="w3-tag w3-wide">Java 7 Automatic Resource Management<br/> in try-with-resources Statement</span></h1>
<!-- 内容 -->

<div style="text-align: right">
    <a title="Table Of Content" href="/archive/java/java_classfile_index.html">TOC</a>
    <a href="javascript:void(0);" onclick="toggle_all_sub_note()">Toggle Commentary</a>
</div>

<h2>Before JDK 7</h2>

<p class="indented">
    Before JDK 7, we need to use a <code>try-catch-finally</code> statement to manage resources.
    We need a <code>finally</code> block, to ensure that the resources are properly closed regardless of whether the <code>try</code> completes normally or abruptly.
</p>

<pre title="HelloWorld.java" class="javacode">
import java.io.*;

public class FileCopyPreJDK7 {
    public static void main(String[] args) {
        BufferedReader src = null;
        BufferedWriter dest = null;
        try {
            src  = new BufferedReader(new FileReader("in.txt"));
            dest = new BufferedWriter(new FileWriter("out.txt"));
            String line;
            while ((line = src.readLine()) != null) {
                System.out.println(line);
                dest.write(line);
                dest.newLine();  // write a newline
            }
        } catch (IOException ex) {
            ex.printStackTrace();
        } finally {            // always close the streams
            closeQuietly(src);
            closeQuietly(dest);
        }
    }

    public static void closeQuietly(Closeable c) {
        if (c != null) {
            try {
                c.close();
            } catch (IOException e) {
                e.printStackTrace();
            }
        }
    }
}
</pre>

<pre title="main:([Ljava/lang/String;)V" hidden class="plaintext">
=== === ===  === === ===  === === ===
Method main:([Ljava/lang/String;)V
=== === ===  === === ===  === === ===
max_stack = 5
max_locals = 5
code_length = 107
code = ......
Exception Table:
from    to  target  type
   4    66      77  java/io/IOException
   4    66      93  All Exceptions(catch_type = 0)
  77    82      93  All Exceptions(catch_type = 0)
  93    95      93  All Exceptions(catch_type = 0)
=== === ===  === === ===  === === ===    "Before Try Clause"
0000: aconst_null          // 01
0001: astore_1             // 4C         // BufferedReader src = null;
0002: aconst_null          // 01
0003: astore_2             // 4D         // BufferedWriter dest = null;
=== === ===  === === ===  === === ===    "Try Clause"
0004: new             #2   // BB0002     || java/io/BufferedReader
0007: dup                  // 59
0008: new             #3   // BB0003     || java/io/FileReader
0011: dup                  // 59
0012: ldc             #4   // 1204       || in.txt
0014: invokespecial   #5   // B70005     || java/io/FileReader.&lt;init&gt;:(Ljava/lang/String;)V
0017: invokespecial   #6   // B70006     || java/io/BufferedReader.&lt;init&gt;:(Ljava/io/Reader;)V
0020: astore_1             // 4C
0021: new             #7   // BB0007     || java/io/BufferedWriter
0024: dup                  // 59
0025: new             #8   // BB0008     || java/io/FileWriter
0028: dup                  // 59
0029: ldc             #9   // 1209       || out.txt
0031: invokespecial   #10  // B7000A     || java/io/FileWriter.&lt;init&gt;:(Ljava/lang/String;)V
0034: invokespecial   #11  // B7000B     || java/io/BufferedWriter.&lt;init&gt;:(Ljava/io/Writer;)V
0037: astore_2             // 4D
0038: aload_1              // 2B
0039: invokevirtual   #12  // B6000C     || java/io/BufferedReader.readLine:()Ljava/lang/String;
0042: dup                  // 59
0043: astore_3             // 4E
0044: ifnull          22   // C60016
0047: getstatic       #13  // B2000D     || java/lang/System.out:Ljava/io/PrintStream;
0050: aload_3              // 2D
0051: invokevirtual   #14  // B6000E     || java/io/PrintStream.println:(Ljava/lang/String;)V
0054: aload_2              // 2C
0055: aload_3              // 2D
0056: invokevirtual   #15  // B6000F     || java/io/BufferedWriter.write:(Ljava/lang/String;)V
0059: aload_2              // 2C
0060: invokevirtual   #16  // B60010     || java/io/BufferedWriter.newLine:()V
0063: goto            -25  // A7FFE7
=== === ===  === === ===  === === ===    "Finally Clause"
0066: aload_1              // 2B
0067: invokestatic    #17  // B80011     || sample/FileCopyPreJDK7.closeQuietly:(Ljava/io/Closeable;)V
0070: aload_2              // 2C
0071: invokestatic    #17  // B80011     || sample/FileCopyPreJDK7.closeQuietly:(Ljava/io/Closeable;)V
0074: goto            32   // A70020
=== === ===  === === ===  === === ===    "Catch Clause"
0077: astore_3             // 4E
0078: aload_3              // 2D
0079: invokevirtual   #19  // B60013     || java/io/IOException.printStackTrace:()V
=== === ===  === === ===  === === ===    "Finally Clause"
0082: aload_1              // 2B
0083: invokestatic    #17  // B80011     || sample/FileCopyPreJDK7.closeQuietly:(Ljava/io/Closeable;)V
0086: aload_2              // 2C
0087: invokestatic    #17  // B80011     || sample/FileCopyPreJDK7.closeQuietly:(Ljava/io/Closeable;)V
0090: goto            16   // A70010
=== === ===  === === ===  === === ===    "All Exceptions Finally Clause"
0093: astore          4    // 3A04
0095: aload_1              // 2B
0096: invokestatic    #17  // B80011     || sample/FileCopyPreJDK7.closeQuietly:(Ljava/io/Closeable;)V
0099: aload_2              // 2C
0100: invokestatic    #17  // B80011     || sample/FileCopyPreJDK7.closeQuietly:(Ljava/io/Closeable;)V
0103: aload           4    // 1904
0105: athrow               // BF
0106: return               // B1
=== === ===  === === ===  === === ===
LocalVariableTable:
index  start_pc  length  name_and_type
    0         0     107  args:[Ljava/lang/String;
    1         2     105  src:Ljava/io/BufferedReader;
    2         4     103  dest:Ljava/io/BufferedWriter;
    3        44      22  line:Ljava/lang/String;
    3        78       4  ex:Ljava/io/IOException;
</pre>

<h2>JDK 7</h2>

<p class="indented">
    JDK 7 introduces a <code>try-with-resources</code> statement, which ensures that each of the resources in <code>try(resources)</code> is properly closed at the end of the statement.
    This results in cleaner codes.
</p>

<pre title="HelloWorld.java" class="javacode">
import java.io.*;

public class FileCopyJDK7 {
    public static void main(String[] args) {
        try (BufferedReader src  = new BufferedReader(new FileReader("in.txt"));
             BufferedWriter dest = new BufferedWriter(new FileWriter("out.txt"))) {
            String line;
            while ((line = src.readLine()) != null) {
                System.out.println(line);
                dest.write(line);
                dest.newLine();
            }
        } catch (IOException ex) {
            ex.printStackTrace();
        }
        // src and dest automatically close.
        // No need for finally to explicitly close the resources.
    }
}
</pre>

<pre title="main:([Ljava/lang/String;)V" hidden class="plaintext">
=== === ===  === === ===  === === ===
Method main:([Ljava/lang/String;)V
=== === ===  === === ===  === === ===
max_stack = 5
max_locals = 10
code_length = 231
code = ......
Exception Table:
from    to  target  type
  79    83      86  java/lang/Throwable
  39    70     105  java/lang/Throwable
  39    70     114  All Exceptions(catch_type = 0)
 125   129     132  java/lang/Throwable
 105   116     114  All Exceptions(catch_type = 0)
 159   163     166  java/lang/Throwable
  19   151     182  java/lang/Throwable
  19   151     187  All Exceptions(catch_type = 0)
 197   201     204  java/lang/Throwable
 182   189     187  All Exceptions(catch_type = 0)
   0   222     225  java/io/IOException
=== === ===  === === ===  === === ===    "try-with-resources"
0000: new             #2   // BB0002     || java/io/BufferedReader
0003: dup                  // 59
0004: new             #3   // BB0003     || java/io/FileReader
0007: dup                  // 59
0008: ldc             #4   // 1204       || in.txt
0010: invokespecial   #5   // B70005     || java/io/FileReader.&lt;init&gt;:(Ljava/lang/String;)V
0013: invokespecial   #6   // B70006     || java/io/BufferedReader.&lt;init&gt;:(Ljava/io/Reader;)V
0016: astore_1             || 4C         "local variable src"
0017: aconst_null          // 01
0018: astore_2             || 4D         "local variable src_exception"
0019: new             #7   // BB0007     || java/io/BufferedWriter
0022: dup                  // 59
0023: new             #8   // BB0008     || java/io/FileWriter
0026: dup                  // 59
0027: ldc             #9   // 1209       || out.txt
0029: invokespecial   #10  // B7000A     || java/io/FileWriter.&lt;init&gt;:(Ljava/lang/String;)V
0032: invokespecial   #11  // B7000B     || java/io/BufferedWriter.&lt;init&gt;:(Ljava/io/Writer;)V
0035: astore_3             || 4E         "local variable dest"
0036: aconst_null          // 01
0037: astore          4    || 3A04       "local variable dest_exception"
0039: aload_1              || 2B         "while clause"
0040: invokevirtual   #12  // B6000C     || java/io/BufferedReader.readLine:()Ljava/lang/String;
0043: dup                  // 59
0044: astore          5    || 3A05       "local variable line"
0046: ifnull          24   || C60018     "jump out of while clause" --&gt; 70
0049: getstatic       #13  // B2000D     || java/lang/System.out:Ljava/io/PrintStream;
0052: aload           5    // 1905
0054: invokevirtual   #14  // B6000E     || java/io/PrintStream.println:(Ljava/lang/String;)V
0057: aload_3              // 2D
0058: aload           5    // 1905
0060: invokevirtual   #15  // B6000F     || java/io/BufferedWriter.write:(Ljava/lang/String;)V
0063: aload_3              // 2D
0064: invokevirtual   #16  // B60010     || java/io/BufferedWriter.newLine:()V
0067: goto            -28  // A7FFE4     --&gt; 39
=== === ===  === === ===  === === ===    "close resource dest"
0070: aload_3              // 2D         "local variable dest"
0071: ifnull          80   // C60050
0074: aload           4    // 1904       "local variable dest_exception"
0076: ifnull          22   // C60016     --&gt; 98
0079: aload_3              // 2D
0080: invokevirtual   #17  // B60011     || java/io/BufferedWriter.close:()V
0083: goto            68   // A70044     --&gt; 151
=== === ===  === === ===  === === ===    "catch dest resource close exception"  79~83:86  java/lang/Throwable
0086: astore          5    // 3A05
0088: aload           4    // 1904
0090: aload           5    // 1905
0092: invokevirtual   #19  // B60013     || java/lang/Throwable.addSuppressed:(Ljava/lang/Throwable;)V
0095: goto            56   // A70038
=== === ===  === === ===  === === ===    "close resource dest"
0098: aload_3              // 2D         "local variable dest"
0099: invokevirtual   #17  // B60011     || java/io/BufferedWriter.close:()V
0102: goto            49   // A70031     --&gt; 151
=== === ===  === === ===  === === ===    "catch Throwable and throw Throwable"  39~70:105  java/lang/Throwable
0105: astore          5    // 3A05
0107: aload           5    // 1905
0109: astore          4    // 3A04
0111: aload           5    // 1905
0113: athrow               // BF         --&gt; 114   105~116:114  All Exceptions
=== === ===  === === ===  === === ===    "catch All Exceptions"  39~70,105~116:114  All Exceptions
0114: astore          6    // 3A06
0116: aload_3              // 2D
0117: ifnull          31   // C6001F     --&gt; 148
0120: aload           4    // 1904
0122: ifnull          22   // C60016     --&gt; 144
=== === ===  === === ===  === === ===    "close resource dest"
0125: aload_3              // 2D         "local variable dest"
0126: invokevirtual   #17  // B60011     || java/io/BufferedWriter.close:()V
0129: goto            19   // A70013     --&gt; 148
=== === ===  === === ===  === === ===    "catch Throwable and suppress"  125~129:132  java/lang/Throwable
0132: astore          7    // 3A07
0134: aload           4    // 1904
0136: aload           7    // 1907
0138: invokevirtual   #19  // B60013     || java/lang/Throwable.addSuppressed:(Ljava/lang/Throwable;)V
0141: goto            7    // A70007
=== === ===  === === ===  === === ===    "close resource dest and throw"
0144: aload_3              // 2D         "local variable dest"
0145: invokevirtual   #17  // B60011     || java/io/BufferedWriter.close:()V
0148: aload           6    // 1906
0150: athrow               // BF         --&gt; 182 19~151:182  java/lang/Throwable
=== === ===  === === ===  === === ===    "close resource src"
0151: aload_1              // 2B         "local variable src"
0152: ifnull          70   // C60046
0155: aload_2              // 2C         "local variable src_exception"
0156: ifnull          19   // C60013     --&gt; 175
0159: aload_1              // 2B         "local variable src"
0160: invokevirtual   #20  // B60014     || java/io/BufferedReader.close:()V
0163: goto            59   // A7003B     --&gt; 222
=== === ===  === === ===  === === ===    "catch Throwable and suppress"  159~163:166  java/lang/Throwable
0166: astore_3             // 4E
0167: aload_2              // 2C
0168: aload_3              // 2D
0169: invokevirtual   #19  // B60013     || java/lang/Throwable.addSuppressed:(Ljava/lang/Throwable;)V
0172: goto            50   // A70032
=== === ===  === === ===  === === ===    "close resource src"
0175: aload_1              // 2B         "local variable src"
0176: invokevirtual   #20  // B60014     || java/io/BufferedReader.close:()V
0179: goto            43   // A7002B     --&gt; 222
=== === ===  === === ===  === === ===    "catch Throwable and throw"  19~151:182  java/lang/Throwable
0182: astore_3             // 4E
0183: aload_3              // 2D
0184: astore_2             // 4D
0185: aload_3              // 2D
0186: athrow               // BF         --&gt; 187   182~189:187  All Exceptions
=== === ===  === === ===  === === ===    "close resource src"  19~151,182~189:187  All Exceptions
0187: astore          8    // 3A08
0189: aload_1              // 2B
0190: ifnull          29   // C6001D
0193: aload_2              // 2C
0194: ifnull          21   // C60015
0197: aload_1              // 2B         "local variable src"
0198: invokevirtual   #20  // B60014     || java/io/BufferedReader.close:()V
0201: goto            18   // A70012     --&gt; 219
=== === ===  === === ===  === === ===    "catch Throwable and suppress"  197~201:204  java/lang/Throwable
0204: astore          9    // 3A09
0206: aload_2              // 2C
0207: aload           9    // 1909
0209: invokevirtual   #19  // B60013     || java/lang/Throwable.addSuppressed:(Ljava/lang/Throwable;)V
0212: goto            7    // A70007
=== === ===  === === ===  === === ===    "close resource src and throw"
0215: aload_1              // 2B         "local variable src"
0216: invokevirtual   #20  // B60014     || java/io/BufferedReader.close:()V
0219: aload           8    // 1908
0221: athrow               // BF         --&gtl 225  0~222:225  java/io/IOException
=== === ===  === === ===  === === ===
0222: goto            8    // A70008     --&gt; 230
=== === ===  === === ===  === === ===    "catch clause"  0~222:225  java/io/IOException
0225: astore_1             // 4C         "local variable ex"
0226: aload_1              // 2B         "local variable ex"
0227: invokevirtual   #22  // B60016     || java/io/IOException.printStackTrace:()V
0230: return               // B1
=== === ===  === === ===  === === ===
LocalVariableTable:
index  start_pc  length  name_and_type
    0         0     231  args:[Ljava/lang/String;
    1        17     205  src:Ljava/io/BufferedReader;
    1       226       4  ex:Ljava/io/IOException;
    3        36     115  dest:Ljava/io/BufferedWriter;
    5        46      24  line:Ljava/lang/String;
</pre>



<h2>Simple Example</h2>

<pre title="HelloWorld.java" class="javacode">
import java.io.FileInputStream;
import java.io.IOException;

public class HelloWorld {
    public void test() {
        try (
                FileInputStream fin = new FileInputStream("/path/to/file");
        ) {
            int ch;
            while ((ch = fin.read()) != -1) {
                System.out.println(ch);
            }
        } catch (IOException e) {
            e.printStackTrace();
        }
    }
}
</pre>

<pre title="test()V" hidden class="plaintext">
=== === ===  === === ===  === === ===
Method test:()V
=== === ===  === === ===  === === ===
max_stack = 3
max_locals = 6
code_length = 112
code = ......
Exception Table:
from    to  target  type
  40    44      47  java/lang/Throwable
  12    32      63  java/lang/Throwable
  12    32      68  All Exceptions(catch_type = 0)
  78    82      85  java/lang/Throwable
  63    70      68  All Exceptions(catch_type = 0)
   0   103     106  java/io/IOException
=== === ===  === === ===  === === ===    "try()"
0000: new             #2   // BB0002     || java/io/FileInputStream
0003: dup                  // 59
0004: ldc             #3   // 1203       || /path/to/file
0006: invokespecial   #4   // B70004     || java/io/FileInputStream.&lt;init&gt;:(Ljava/lang/String;)V
0009: astore_1             // 4C
0010: aconst_null          // 01
0011: astore_2             // 4D
=== === ===  === === ===  === === ===    "try body"
0012: aload_1              // 2B
0013: invokevirtual   #5   // B60005     || java/io/FileInputStream.read:()I
0016: dup                  // 59
0017: istore_3             // 3E
0018: iconst_m1            // 02
0019: if_icmpeq       13   // 9F000D     --&gt; 32
0022: getstatic       #6   // B20006     || java/lang/System.out:Ljava/io/PrintStream;
0025: iload_3              // 1D
0026: invokevirtual   #7   // B60007     || java/io/PrintStream.println:(I)V
0029: goto            -17  // A7FFEF
=== === ===  === === ===  === === ===    "after try"
0032: aload_1              // 2B
0033: ifnull          70   // C60046     --&gt; 103
0036: aload_2              // 2C
0037: ifnull          19   // C60013     --&gt; 56
0040: aload_1              // 2B
0041: invokevirtual   #8   // B60008     || java/io/FileInputStream.close:()V
0044: goto            59   // A7003B
=== === ===  === === ===  === === ===
0047: astore_3             // 4E
0048: aload_2              // 2C
0049: aload_3              // 2D
0050: invokevirtual   #10  // B6000A     || java/lang/Throwable.addSuppressed:(Ljava/lang/Throwable;)V
0053: goto            50   // A70032
=== === ===  === === ===  === === ===    "close file resource"
0056: aload_1              // 2B
0057: invokevirtual   #8   // B60008     || java/io/FileInputStream.close:()V
0060: goto            43   // A7002B     --&gt; 103
=== === ===  === === ===  === === ===
0063: astore_3             // 4E
0064: aload_3              // 2D
0065: astore_2             // 4D
0066: aload_3              // 2D
0067: athrow               // BF
=== === ===  === === ===  === === ===
0068: astore          4    // 3A04
0070: aload_1              // 2B
0071: ifnull          29   // C6001D     --&gt; 100
0074: aload_2              // 2C
0075: ifnull          21   // C60015     --&gt; 96
0078: aload_1              // 2B
0079: invokevirtual   #8   // B60008     || java/io/FileInputStream.close:()V
0082: goto            18   // A70012     --&gt; 100
=== === ===  === === ===  === === ===
0085: astore          5    // 3A05
0087: aload_2              // 2C
0088: aload           5    // 1905
0090: invokevirtual   #10  // B6000A     || java/lang/Throwable.addSuppressed:(Ljava/lang/Throwable;)V
0093: goto            7    // A70007
=== === ===  === === ===  === === ===
0096: aload_1              // 2B
0097: invokevirtual   #8   // B60008     || java/io/FileInputStream.close:()V
0100: aload           4    // 1904
0102: athrow               // BF
=== === ===  === === ===  === === ===
0103: goto            8    // A70008     --&gt; 111
=== === ===  === === ===  === === ===    "catch clause"
0106: astore_1             // 4C
0107: aload_1              // 2B
0108: invokevirtual   #12  // B6000C     || java/io/IOException.printStackTrace:()V
0111: return               // B1
=== === ===  === === ===  === === ===
LocalVariableTable:
index  start_pc  length  name_and_type
    0         0     112  this:Lsample/HelloWorld;
    1        10      93  fin:Ljava/io/FileInputStream;
    1       107       4  e:Ljava/io/IOException;
    3        18      14  ch:I
</pre>

<p>
    梳理思路：
</p>

<ol>
    <li>正常执行，不出现任何Exception，如何执行</li>
    <li>出现Exception，如何执行</li>
</ol>

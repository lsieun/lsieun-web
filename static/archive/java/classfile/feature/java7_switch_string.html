<!-- 标题 -->
<h1 class="w3-center w3-padding-64 w3-tangerine"><span class="w3-tag w3-wide">Java 7 switch String</span></h1>
<!-- 内容 -->

<div style="text-align: right">
    <a title="Table Of Content" href="/archive/java/java_classfile_index.html">TOC</a>
    <a href="javascript:void(0);" onclick="toggle_all_sub_note()">Toggle Commentary</a>
</div>

<h2>switch String Example</h2>

<p>
    Before JDK 7, only integral types (such as <code>int</code>, <code>char</code>) or an enum type can be used as selector for switch-case statement.
    In JDK 7, you can use a <code>String</code> object as the selector. For example,
</p>

<div title="HelloWorld.java" class="javacode">
public class HelloWorld {
    public void test(String direction) {
        switch (direction) {   // switch on String selector
            case "NE":
                System.out.println("North East");
                break;
            case "SE":
                System.out.println("South East");
                break;
            case "NW":
                System.out.println("North West");
                break;
            case "SW":
                System.out.println("South West");
                break;
            default:
                System.out.println("Invalid");
        }
    }
}
</div>

<div title="test:(Ljava/lang/String;)V" class="plaintext">
=== === ===  === === ===  === === ===
0000: aload_1              // 2B
0001: astore_2             // 4D
0002: iconst_m1            // 02
0003: istore_3             // 3E
0004: aload_2              // 2C
0005: invokevirtual   #2   // B60002     || java/lang/String.hashCode:()I
0008: lookupswitch    97   // AB0000000000006100000004000009B70000002C000009C90000004800000A520000003A00000A6400000056
      {
           2487: 44
           2505: 72
           2642: 58
           2660: 86
        default: 97
      }
0052: aload_2              // 2C
0053: ldc             #3   // 1203       || NE
0055: invokevirtual   #4   // B60004     || java/lang/String.equals:(Ljava/lang/Object;)Z
0058: ifeq            47   // 99002F
0061: iconst_0             // 03
0062: istore_3             // 3E
0063: goto            42   // A7002A
0066: aload_2              // 2C
0067: ldc             #5   // 1205       || SE
0069: invokevirtual   #4   // B60004     || java/lang/String.equals:(Ljava/lang/Object;)Z
0072: ifeq            33   // 990021
0075: iconst_1             // 04
0076: istore_3             // 3E
0077: goto            28   // A7001C
0080: aload_2              // 2C
0081: ldc             #6   // 1206       || NW
0083: invokevirtual   #4   // B60004     || java/lang/String.equals:(Ljava/lang/Object;)Z
0086: ifeq            19   // 990013
0089: iconst_2             // 05
0090: istore_3             // 3E
0091: goto            14   // A7000E
0094: aload_2              // 2C
0095: ldc             #7   // 1207       || SW
0097: invokevirtual   #4   // B60004     || java/lang/String.equals:(Ljava/lang/Object;)Z
0100: ifeq            5    // 990005
0103: iconst_3             // 06
0104: istore_3             // 3E
0105: iload_3              // 1D
0106: tableswitch     74   // AA000000004A00000000000000030000001E00000029000000340000003F
      {
              0: 30
              1: 41
              2: 52
              3: 63
        default: 74
      }
0136: getstatic       #8   // B20008     || java/lang/System.out:Ljava/io/PrintStream;
0139: ldc             #9   // 1209       || North East
0141: invokevirtual   #10  // B6000A     || java/io/PrintStream.println:(Ljava/lang/String;)V
0144: goto            44   // A7002C
0147: getstatic       #8   // B20008     || java/lang/System.out:Ljava/io/PrintStream;
0150: ldc             #11  // 120B       || South East
0152: invokevirtual   #10  // B6000A     || java/io/PrintStream.println:(Ljava/lang/String;)V
0155: goto            33   // A70021
0158: getstatic       #8   // B20008     || java/lang/System.out:Ljava/io/PrintStream;
0161: ldc             #12  // 120C       || North West
0163: invokevirtual   #10  // B6000A     || java/io/PrintStream.println:(Ljava/lang/String;)V
0166: goto            22   // A70016
0169: getstatic       #8   // B20008     || java/lang/System.out:Ljava/io/PrintStream;
0172: ldc             #13  // 120D       || South West
0174: invokevirtual   #10  // B6000A     || java/io/PrintStream.println:(Ljava/lang/String;)V
0177: goto            11   // A7000B
0180: getstatic       #8   // B20008     || java/lang/System.out:Ljava/io/PrintStream;
0183: ldc             #14  // 120E       || Invalid
0185: invokevirtual   #10  // B6000A     || java/io/PrintStream.println:(Ljava/lang/String;)V
0188: return               // B1
=== === ===  === === ===  === === ===
LocalVariableTable:
index  start_pc  length  name_and_type
    0         0     189  this:Lsample/HelloWorld;
    1         0     189  direction:Ljava/lang/String;
</div>

<h2>String.hashCode:()I</h2>

<div title="HelloWorld.java" class="javacode">
public class HelloWorld {
    public static void main(String[] args) {
        System.out.println("NE".hashCode()); // 2487
        System.out.println("SE".hashCode()); // 2642
        System.out.println("NW".hashCode()); // 2505
        System.out.println("SW".hashCode()); // 2660
    }
}
</div>

<h2>switch int</h2>

<div title="HelloWorld.java" class="javacode">
public class HelloWorld {
    public void test(String direction) {
        String copy = direction;
        int val = -1;
        switch (copy.hashCode()) {
            case 2487:
                if (copy.equals("NE")) {
                    val = 0;
                }
                break;
            case 2505:
                if (copy.equals("NW")) {
                    val = 2;
                }
                break;
            case 2642:
                if (copy.equals("SE")) {
                    val = 1;
                }
                break;
            case 2660:
                if (copy.equals("SW")) {
                    val = 3;
                }
                break;
            default:
                break;
        }
        switch (val) {
            case 0:
                System.out.println("North East");
                break;
            case 1:
                System.out.println("South East");
                break;
            case 2:
                System.out.println("North West");
                break;
            case 3:
                System.out.println("South West");
                break;
            default:
                System.out.println("Invalid");
                break;
        }
    }
}
</div>

<div title="test:(Ljava/lang/String;)V" hidden class="plaintext">
=== === ===  === === ===  === === ===
0000: aload_1              // 2B
0001: astore_2             // 4D
0002: iconst_m1            // 02
0003: istore_3             // 3E
0004: aload_2              // 2C
0005: invokevirtual   #2   // B60002     || java/lang/String.hashCode:()I
0008: lookupswitch    100  // AB0000000000006400000004000009B70000002C000009C90000003A00000A520000004800000A6400000056
      {
           2487: 44
           2505: 58
           2642: 72
           2660: 86
        default: 100
      }
0052: aload_2              // 2C
0053: ldc             #3   // 1203       || NE
0055: invokevirtual   #4   // B60004     || java/lang/String.equals:(Ljava/lang/Object;)Z
0058: ifeq            50   // 990032
0061: iconst_0             // 03
0062: istore_3             // 3E
0063: goto            45   // A7002D
0066: aload_2              // 2C
0067: ldc             #5   // 1205       || NW
0069: invokevirtual   #4   // B60004     || java/lang/String.equals:(Ljava/lang/Object;)Z
0072: ifeq            36   // 990024
0075: iconst_2             // 05
0076: istore_3             // 3E
0077: goto            31   // A7001F
0080: aload_2              // 2C
0081: ldc             #6   // 1206       || SE
0083: invokevirtual   #4   // B60004     || java/lang/String.equals:(Ljava/lang/Object;)Z
0086: ifeq            22   // 990016
0089: iconst_1             // 04
0090: istore_3             // 3E
0091: goto            17   // A70011
0094: aload_2              // 2C
0095: ldc             #7   // 1207       || SW
0097: invokevirtual   #4   // B60004     || java/lang/String.equals:(Ljava/lang/Object;)Z
0100: ifeq            8    // 990008
0103: iconst_3             // 06
0104: istore_3             // 3E
0105: goto            3    // A70003
0108: iload_3              // 1D
0109: tableswitch     75   // AA00000000004B00000000000000030000001F0000002A0000003500000040
      {
              0: 31
              1: 42
              2: 53
              3: 64
        default: 75
      }
0140: getstatic       #8   // B20008     || java/lang/System.out:Ljava/io/PrintStream;
0143: ldc             #9   // 1209       || North East
0145: invokevirtual   #10  // B6000A     || java/io/PrintStream.println:(Ljava/lang/String;)V
0148: goto            44   // A7002C
0151: getstatic       #8   // B20008     || java/lang/System.out:Ljava/io/PrintStream;
0154: ldc             #11  // 120B       || South East
0156: invokevirtual   #10  // B6000A     || java/io/PrintStream.println:(Ljava/lang/String;)V
0159: goto            33   // A70021
0162: getstatic       #8   // B20008     || java/lang/System.out:Ljava/io/PrintStream;
0165: ldc             #12  // 120C       || North West
0167: invokevirtual   #10  // B6000A     || java/io/PrintStream.println:(Ljava/lang/String;)V
0170: goto            22   // A70016
0173: getstatic       #8   // B20008     || java/lang/System.out:Ljava/io/PrintStream;
0176: ldc             #13  // 120D       || South West
0178: invokevirtual   #10  // B6000A     || java/io/PrintStream.println:(Ljava/lang/String;)V
0181: goto            11   // A7000B
0184: getstatic       #8   // B20008     || java/lang/System.out:Ljava/io/PrintStream;
0187: ldc             #14  // 120E       || Invalid
0189: invokevirtual   #10  // B6000A     || java/io/PrintStream.println:(Ljava/lang/String;)V
0192: return               // B1
=== === ===  === === ===  === === ===
LocalVariableTable:
index  start_pc  length  name_and_type
    0         0     193  this:Lsample/HelloWorld;
    1         0     193  direction:Ljava/lang/String;
    2         2     191  copy:Ljava/lang/String;
    3         4     189  val:I
</div>

<h2>Summing Up</h2>

<p>
    在switch中使用String经过两个步骤：
</p>

<ol>
    <li>计算String的HashCode</li>
    <li>调用String的equals方法</li>
</ol>
